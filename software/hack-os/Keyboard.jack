/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int keyboard;

    /** Initializes the keyboard. */
    function void init() {
        let keyboard = 24576;
        
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(keyboard);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c;
        while(c = 0) {
            let c = Keyboard.keyPressed();
        }

        while(~(Keyboard.keyPressed() = 0)) {}

        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var char c;
        var String string;

        let string = String.new(60);
        do Output.printString(message);

        let c = Keyboard.readChar();
        while((~(c = 128)) & (~(string.length() > 60))) {
            if(c = 129) {
                do Output.backSpace();
                do Output.printChar(32); // 32 == space
                do Output.backSpace();
                do string.eraseLastChar();
            } else {
                do string.appendChar(c);
                do Output.printChar(c);
            }

            let c = Keyboard.readChar();
        }

        do Output.println();
        return string;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var char c;
        var int n, i;
        var String number;

        let number = String.new(60);
        do Output.printString(message);

        let c = Keyboard.readChar();
        while((~(c = 128)) & (~(number.length() > 60))) {
            if(c = 129) {
                do Output.backSpace();
                do Output.printChar(32); // 32 == space
                do Output.backSpace();
                do number.eraseLastChar();
            } else {
                if(((c > 47) & (c < 58)) | (c = 45)) {
                    do number.appendChar(c);
                    do Output.printChar(c);
                }
            }

            let c = Keyboard.readChar();
        }

        do Output.println();
        return number.intValue();
    }
}
